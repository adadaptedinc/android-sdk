import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id("org.barfuin.gradle.jacocolog") version "1.2.1"
    id 'maven-publish'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.10'
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply from: 'jacoco.gradle'

group='com.adadapted'

android {
    compileSdk 34
    namespace = 'com.adadapted'

    defaultConfig {
        minSdkVersion 23
        //noinspection EditedTargetSdkVersion
        targetSdkVersion 34
        versionName "3.4.5"
        buildConfigField("String","VERSION_NAME", "\"${defaultConfig.versionName}\"")
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles 'aasdk-proguard-rules.pro'
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'aasdk-proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.gms:play-services-ads-identifier:18.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.9.10"

    implementation("io.ktor:ktor-client-core:2.2.4")
    implementation("io.ktor:ktor-serialization-kotlinx-json:2.2.4")
    implementation("io.ktor:ktor-client-content-negotiation:2.2.4")

    // DateTime
    implementation 'org.jetbrains.kotlinx:kotlinx-datetime:0.4.0'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.9.10'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.1'

    implementation 'com.google.android.material:material:1.9.0'
    implementation 'io.ktor:ktor-client-android:2.2.4'
    implementation 'com.google.android.gms:play-services-ads-identifier:18.0.1'
    implementation("io.github.aakira:napier:2.6.1")

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.ktor:ktor-client-mock2.2.4'
    testImplementation 'org.json:json:20200518'
    testImplementation 'org.robolectric:robolectric:4.10.3'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.1'
    testImplementation project(path: ':advertising_sdk')
}

repositories {
    mavenCentral()
}

kotlin {
    explicitApiWarning()
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.gitlab.adadapted'
                artifactId = 'android-sdk'
                version = '3.4.5'
            }
        }
    }
}

tasks.withType(Test) {
    testLogging {
        events TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true

        debug {
            events TestLogEvent.STARTED,
                    TestLogEvent.FAILED,
                    TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED,
                    TestLogEvent.STANDARD_ERROR,
                    TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) {
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}